// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int KEYBOARD;

    /** Initializes the keyboard. */
    function void init() 
    {
        let KEYBOARD = 24576;
        return;
    } 

    /**
     * Returns the ASCII code (as char) of the currently pressed key,
     * or 0 if no key is currently pressed.
     * Recognizes all ASCII characters, as well as the following extension
     * of action keys:
     * New line = 128 = String.newline()
     * Backspace = 129 = String.backspace()
     * Left Arrow = 130
     * Up Arrow = 131
     * Right Arrow = 132
     * Down Arrow = 133
     * Home = 134
     * End = 135
     * Page Up = 136
     * Page Down = 137
     * Insert = 138
     * Delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() 
    {
        var int keyVal;
        let keyVal = Memory.peek(24576);
        if (keyVal < 153)
        {
            return keyVal;
        }
        return 0;
    }

    /**								
     * Reads the next character from the keyboard.
     * waits until a key is pressed and then released, then echoes
     * the key to the screen, and returns the value of the pressed key.
     */
    function char readChar() 
    {
        var char old, new;
        do Output.printChar(0);
        
        while(new = 0)
        {
            let new = Keyboard.keyPressed();
        }
        let old = new;
        while(new = old)
        {
            let new = Keyboard.keyPressed();
        }
        if(old < String.newLine())
        {
            do Output.backSpace();
            do Output.printChar(old);
        }
        else
        {
            do Output.backSpace();
        }
        return old;
    }

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its value.
     */
    function String readLine(String message) 
    {
        var char c;
        var String line;
        let line = String.new(80);
        do Output.printString(message);

        while(true)
        {
            let c = Keyboard.readChar();
            if(c = String.newLine()) 
            {
                do Output.println();
                return line;
            }
            if(c = String.backSpace())
            {
                do Output.backSpace();
                do line.eraseLastChar();
            }
            else
            {
                do line.appendChar(c);
            }

        }
        return line;
    }   

    /**								
     * Prints the message on the screen, reads the next line
     * (until a newline character) from the keyboard, and returns its
     * integer value (until the first non numeric character).
     */
    function int readInt(String message) 
    {
        var String lineRead;
        let lineRead = Keyboard.readLine(message);
        return lineRead.intValue();
    }
}
