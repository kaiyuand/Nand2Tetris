// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
	Mux16(a=x,b[0..15]=false,sel=zx,out=w1);   //Add the function of zx and zy
	Mux16(a=y,b[0..15]=false,sel=zy,out=w2);
	
	Not16(in=w1,out=nw1);                      //Add the function of nx and ny
	Not16(in=w2,out=nw2);
	Mux16(a=w1,b=nw1,sel=nx,out=w3);
	Mux16(a=w2,b=nw2,sel=ny,out=w4);
	
	And16(a=w3,b=w4,out=w5);                   //Add the function of f
	Add16(a=w3,b=w4,out=w6);
	Mux16(a=w5,b=w6,sel=f,out=w7);

	Not16(in=w7,out=nw7);                      //Add the function of no
	Mux16(a=w7,b=nw7,sel=no,out=w8);              
	Mux16(a=w7,b=nw7,sel=no,out=out);
	
	Or16Way(in=w8,out=nzr);                    //Add the function of zr
	Not(in=nzr,out=zr);		           //the Or16Way is defined by myself

	And16(a=w8,b[15]=true,b[0..14]=false,out=ng1);     //Add the function of ng
	Or16Way(in=ng1,out=ng);
}